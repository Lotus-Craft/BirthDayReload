plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1' // Плагін для shading
}

group = 'org.referix'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "mojang"
        url = "https://libraries.minecraft.net/"
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven { url 'https://repo.luckperms.net/' }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"
    implementation 'com.mojang:authlib:1.5.21'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    // JDBI Core
    implementation 'org.jdbi:jdbi3-core:3.38.0'

    // SQLite JDBC драйвер
    implementation 'org.xerial:sqlite-jdbc:3.45.1.0'

    // Логування для JDBI (опціонально, для налагодження)
    implementation 'org.jdbi:jdbi3-sqlobject:3.38.0'
    runtimeOnly 'org.slf4j:slf4j-api:2.0.7'

    // JDA
    implementation("net.dv8tion:JDA:5.0.0-beta.22") {
        exclude group: "com.fasterxml.jackson.core" // Убираем JSON обработчик
        exclude group: "com.squareup.okhttp3"       // Убираем HTTP-клиент, если не используете REST
        exclude group: "org.apache.commons"         // Commons Utils
        exclude module: "opus-java"                 // Убираем аудио
        exclude group: "org.slf4j"                  // Если используете другой логгер
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

// ShadowJar конфігурація
shadowJar {
    archiveBaseName.set('BIRTHDAY') // Назва файлу
    archiveVersion.set('1.0.0-DEV') // Версія файлу
    archiveClassifier.set('') // Без класифікатора
    minimize()
    exclude 'org/sqlite/native/**'
    relocate 'org.jdbi', 'org.referix.libs.jdbi'
    relocate 'org.sqlite', 'org.referix.libs.sqlite'
}

build.dependsOn shadowJar
